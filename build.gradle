plugins {
    id 'java-gradle-plugin'
}

group "com.typelead"

static def getGitRev() {
    def proc = "git rev-parse --short HEAD".execute()
    proc.waitFor()
    return proc.text.trim()
}

static def isGitDirty() {
    return "git diff-index --quiet HEAD".execute().waitFor() != 0
}

version = file('version.txt').text.trim().with {
    if (it.endsWith("SNAPSHOT")) {
        if (isGitDirty()) {
            "$it-${getGitRev()}-dirty"
        } else {
            "$it-${getGitRev()}"
        }
    } else {
        it
    }
}

task printVersion {
    doLast {
        println("gradle-eta version: $version")
    }
}

project.afterEvaluate {
    publishToMavenLocal {
        dependsOn printVersion
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

configurations {
    provided
}

sourceSets {
    main { compileClasspath += configurations.provided }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.google.com' }
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    provided 'com.android.tools.build:gradle:3.0.0'

    testCompile gradleTestKit()
    testCompile 'junit:junit:4.12'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4', {
        exclude module: 'groovy-all'
    }
}
